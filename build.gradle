plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.8'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.graalvm.buildtools.native' version '0.10.6'
    id 'com.github.davidmc24.gradle.plugin.avro' version '1.9.1'
}

group = 'com.twenty9ine'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    mapstructVersion = '1.6.3'
    lombokMapstructBindingVersion = '0.2.0'
    droolsVersion = '10.1.0'
    kieVersion = '7.74.1.Final'
    resilience4jVersion = '2.2.0'
    caffeineVersion = '3.1.8'
    redissonVersion = '3.52.0'
    apicurioVersion = '2.6.13.Final'
    springdocVersion = '2.7.0'
    uuidGeneratorVersion = '5.1.1'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.flywaydb:flyway-database-postgresql'

    //For UUIDv7 (defined in RFC 9562)
    implementation "com.fasterxml.uuid:java-uuid-generator:$uuidGeneratorVersion"
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-database-postgresql'
    implementation 'org.springframework.kafka:spring-kafka'
    implementation "io.apicurio:apicurio-registry-serdes-avro-serde:$apicurioVersion"

    implementation 'org.springframework.integration:spring-integration-core'
    implementation 'org.springframework.integration:spring-integration-kafka'
    implementation 'org.springframework.integration:spring-integration-redis'

    implementation "io.github.resilience4j:resilience4j-spring-boot3:$resilience4jVersion"
    implementation "io.github.resilience4j:resilience4j-circuitbreaker:$resilience4jVersion"
    implementation "io.github.resilience4j:resilience4j-retry:$resilience4jVersion"
    implementation "io.github.resilience4j:resilience4j-bulkhead:$resilience4jVersion"
    implementation "io.github.resilience4j:resilience4j-timelimiter:$resilience4jVersion"

    implementation "com.github.ben-manes.caffeine:caffeine:$caffeineVersion"
    implementation "org.redisson:redisson-spring-boot-starter:$redissonVersion"

    implementation "org.drools:drools-core:$droolsVersion"
    implementation "org.drools:drools-compiler:$droolsVersion"
    implementation "org.drools:drools-verifier:$droolsVersion"
    implementation "org.drools:drools-mvel:$droolsVersion"
    testImplementation "org.drools:drools-xml-support:$droolsVersion"
    implementation "org.kie:kie-spring:$kieVersion"

    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'io.micrometer:micrometer-tracing-bridge-otel'
    implementation 'io.opentelemetry:opentelemetry-exporter-zipkin'

    implementation "org.mapstruct:mapstruct:$mapstructVersion"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$springdocVersion"

    // AWS SageMaker Runtime
    implementation platform('software.amazon.awssdk:bom:2.28.11')
    implementation 'software.amazon.awssdk:sagemakerruntime'
    implementation 'software.amazon.awssdk:sagemakerfeaturestoreruntime'
    implementation 'software.amazon.awssdk:sagemaker'
    implementation 'software.amazon.awssdk:auth'
    implementation 'software.amazon.awssdk:sts'

    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
    implementation 'org.postgresql:postgresql'

    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:$lombokMapstructBindingVersion"
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.0'
    testImplementation 'org.testcontainers:kafka'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:localstack:1.19.3'
    testImplementation 'cloud.localstack:localstack-utils:0.2.23'

    testImplementation 'org.awaitility:awaitility:4.2.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testAnnotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"
}

avro {
    outputCharacterEncoding = 'UTF-8'
    stringType = 'String'
    fieldVisibility = 'PRIVATE'
    enableDecimalLogicalType = true
}

// Task to generate Avro Java classes
//./gradlew generateAvroJava

// Configure source sets for generated Avro classes
sourceSets {
    main {
        java {
            srcDir 'build/generated-main-avro-java'
        }
    }
}

tasks.register('generateDroolsMetadata') {
    doLast {
        def metaInfDir = file("${buildDir}/resources/main/META-INF/maven/${project.group}/${project.name}")
        metaInfDir.mkdirs()

        def propsFile = new File(metaInfDir, "pom.properties")
        propsFile.text = """groupId=${project.group}
                            artifactId=${project.name}
                            version=${project.version}
                        """
    }
}

processResources.finalizedBy generateDroolsMetadata

tasks.named('test') {
    useJUnitPlatform()

    // Enable parallel execution at Gradle level
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1

    // Increase memory for parallel tests
    minHeapSize = "512m"
    maxHeapSize = "2g"

    // JVM arguments for better performance
    jvmArgs = [
            '-XX:+UseParallelGC',
            '-XX:MaxGCPauseMillis=200'
    ]

    // Enable Testcontainers reuse
    systemProperties = [
            'testcontainers.reuse.enable': 'true'
    ]

    // Better test output
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = 'full'
        showStandardStreams = false

        afterSuite { desc, result ->
            if (!desc.parent) {
                println "\n═══════════════════════════════════════════"
                println "Test Results: ${result.resultType}"
                println "  Total Tests: ${result.testCount}"
                println "  Passed: ${result.successfulTestCount}"
                println "  Failed: ${result.failedTestCount}"
                println "  Skipped: ${result.skippedTestCount}"
                println "  Duration: ${(result.endTime - result.startTime) / 1000}s"
                println "═══════════════════════════════════════════\n"
            }
        }
    }
}

tasks.named("bootBuildImage") {
    imageName = "${project.name}"
    environment = ["BP_LIVE_RELOAD_ENABLED": "true",
                   "BP_JVM_VERSION"        : "25.*"]
    imagePlatform = "linux/arm64"
}

